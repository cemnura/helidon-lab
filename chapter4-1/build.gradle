plugins {
    id 'java'
}

group 'com.cemnura'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    // Helidon Webserver
    implementation "io.helidon.webserver:helidon-webserver:$helidonVersion"

    // Helidon Prometheus Support
    implementation "io.helidon.metrics:helidon-metrics-prometheus:$helidonVersion"

    // YAML Configuration Support
    implementation "io.helidon.config:helidon-config-yaml:$helidonVersion"

    // Helidon JSON Support
    implementation "io.helidon.media.jsonp:helidon-media-jsonp-server:$helidonVersion"

    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


// define a custom task to copy all dependencies in the runtime classpath
task copyLibs(type: Copy) {
    from configurations.compileClasspath
    into 'build/libs/libs'
}


jar {

    dependsOn copyLibs

    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Main-Class": 'com.cemnura.server.MetricServer',
                'Class-Path': configurations.compileClasspath.files.collect { "libs/$it.name" }.join(' ')
        )
    }
}
